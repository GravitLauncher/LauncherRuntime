plugins {
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'idea'
    id 'eclipse'
}
javafx {
    version = "12"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.swing', 'javafx.web']
}
sourceCompatibility = '1.8'
targetCompatibility = '1.8'
group 'pro.gravit.launcher'
version '2.1.0'
def mainClassName = "pro.gravit.launcher.client.JavaRuntimeModule"

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.incremental = true // one flag, and things will get MUCH faster    
}

jar {
    manifest.attributes("Module-Main-Class": mainClassName,
            "Module-Config-Class": "pro.gravit.launcher.client.gui.config.GuiModuleConfig",
            "Module-Config-Name": "JavaRuntime")
    jar.archiveFileName = "JavaRuntime_lmodule.jar"
}

task runDev(type: JavaExec) {
    dependsOn assemble
    group = "Execution"
    description = "Run the main class with JavaExecTask"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ["-Dlauncherdebug.modules="+mainClassName, "-Dlauncherdebug.env=DEBUG"]
    mainClass = "pro.gravit.launcher.debug.DebugMain"
}

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation("com.squareup.okhttp3:okhttp:3.12.0")
    implementation 'com.squareup.okio:okio:1.11.0'

    implementation "pro.gravit.launcher:launcher-core:5.3.0"
    implementation "pro.gravit.launcher:launcher-ws-api:5.3.0"
    implementation "pro.gravit.launcher:launcher-client-api:5.3.0"
    implementation "pro.gravit.utils.enfs:enfs:1.0.0-SNAPSHOT"
    implementation 'io.netty:netty-codec-http:4.1.67.Final'
    implementation 'com.github.oshi:oshi-core:5.8.1'
    implementation 'com.google.code.gson:gson:2.8.8'

    api 'io.github.typhon0:AnimateFX:1.2.3'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

defaultTasks 'build'

if (hasProperty('mavenUsername') && hasProperty('mavenPassword') && hasProperty('mavenReleaseRepository') && hasProperty('mavenSnapshotRepository')) {
    publishing {
        repositories {
            maven {
                url = version.endsWith('SNAPSHOT') ? getProperty('mavenSnapshotRepository') : getProperty('mavenReleaseRepository')
                credentials {
                    username getProperty('mavenUsername')
                    password getProperty('mavenPassword')
                }
            }
        }
    }
}
publishing {
    publications {
        javaruntimeapi(MavenPublication) {
            artifactId = 'javaruntime-api'
            artifact(jar) {
                classifier ""
            }
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'GravitLauncher JavaRuntime API'
                description = 'GravitLauncher JavaRuntime Module API'
                url = 'https://launcher.gravit.pro'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://mit-license.org'
                    }
                }
                developers {
                    developer {
                        id = 'gravit'
                        name = 'Gravit'
                        email = 'gravit.min@ya.ru'
                    }
                    developer {
                        id = 'zaxar163'
                        name = 'Zaxar163'
                        email = 'zahar.vcherachny@yandex.ru'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/GravitLauncher/LauncherRuntime.git'
                    developerConnection = 'scm:git:ssh://git@github.com:GravitLauncher/LauncherRuntime.git'
                    url = 'https://launcher.gravit.pro/'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.javaruntimeapi
}
