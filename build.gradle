plugins {
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'idea'
    id 'eclipse'
}
javafx {
    version = "22"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.swing', 'javafx.web']
}
sourceCompatibility = '17'
targetCompatibility = '17'
group 'pro.gravit.launcher'
version '4.0.6'
def mainClassName = "pro.gravit.launcher.gui.JavaRuntimeModule"

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.incremental = true // one flag, and things will get MUCH faster    
}

jar {
    manifest.attributes("Module-Main-Class": mainClassName,
            "Module-Config-Class": "pro.gravit.launcher.gui.config.GuiModuleConfig",
            "Module-Config-Name": "JavaRuntime",
            "Required-Modern-Java": "true")
    jar.archiveFileName = "JavaRuntime_lmodule.jar"
}

task runDev(type: JavaExec) {
    dependsOn assemble
    group = "Execution"
    description = "Run the main class with JavaExecTask"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ["-Dlauncherdebug.modules="+mainClassName, "-Dlauncherdebug.env=DEBUG"]
    mainClass = "pro.gravit.launcher.runtime.debug.DebugMain"
}

repositories {
    mavenLocal()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url "https://maven.gravitlauncher.com"
    }
    mavenCentral()
}

dependencies {
    implementation "pro.gravit.launcher:launcher-core:5.6.9"
    implementation "pro.gravit.launcher:launcher-ws-api:5.6.9"
    implementation "pro.gravit.launcher:launcher-client-api:5.6.9"
    implementation "pro.gravit.launcher:launcher-client-start-api:5.6.9"
    implementation "pro.gravit.launcher:launcher-client-starter-api:5.6.9"
    implementation "pro.gravit.utils.enfs:enfs:2.0.1-SNAPSHOT"
    implementation 'io.netty:netty-codec-http:4.1.67.Final'
    implementation 'com.github.oshi:oshi-core:5.8.1'
    implementation 'com.google.code.gson:gson:2.10.1'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

defaultTasks 'build'

if (hasProperty('mavenUsername') && hasProperty('mavenPassword') && hasProperty('mavenReleaseRepository') && hasProperty('mavenSnapshotRepository')) {
    publishing {
        repositories {
            maven {
                url = version.endsWith('SNAPSHOT') ? getProperty('mavenSnapshotRepository') : getProperty('mavenReleaseRepository')
                credentials {
                    username getProperty('mavenUsername')
                    password getProperty('mavenPassword')
                }
            }
        }
    }
}
publishing {
    publications {
        javaruntimeapi(MavenPublication) {
            artifactId = 'javaruntime-api'
            artifact(jar) {
                classifier ""
            }
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'GravitLauncher JavaRuntime API'
                description = 'GravitLauncher JavaRuntime Module API'
                url = 'https://launcher.gravit.pro'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://mit-license.org'
                    }
                }
                developers {
                    developer {
                        id = 'gravit'
                        name = 'Gravit'
                        email = 'gravit.min@ya.ru'
                    }
                    developer {
                        id = 'zaxar163'
                        name = 'Zaxar163'
                        email = 'zahar.vcherachny@yandex.ru'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/GravitLauncher/LauncherRuntime.git'
                    developerConnection = 'scm:git:ssh://git@github.com:GravitLauncher/LauncherRuntime.git'
                    url = 'https://launcher.gravit.pro/'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.javaruntimeapi
}
